plugins {
    id("fabric-loom") version "0.11.+"
    id("maven-publish")
    id("io.github.juuxel.loom-quiltflower") version "1.6.0"
    id "com.github.breadmoirai.github-release" version "2.4.1"
    id "com.modrinth.minotaur" version "2.+"
    id 'me.hypherionmc.cursegradle' version '2.0.1'
}
apply from: 'https://raw.githubusercontent.com/UltrusBot/GradleScripts/1deb0dd4b4553d7845e21442227abb4c29b17ee8/release.gradle'

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = "Quilt"
        url = uri("https://maven.quiltmc.org/repository/release")
    }
    maven {
        name = "Quilt Snapshots"
        url = uri("https://maven.quiltmc.org/repository/snapshot")
    }
    maven {
        url "https://api.modrinth.com/maven"
    }
    maven {
        url "https://maven.terraformersmc.com"
    }
}

dependencies {
    minecraft(libs.minecraft)
    mappings(variantOf(libs.quilt.mappings) { classifier("intermediary-v2") })
    modImplementation(libs.fabric.loader)
    modImplementation(libs.fabric.api)
    modImplementation(libs.midnightlib)
    include(libs.midnightlib)
    modImplementation(libs.modmenu)
}

var mod = createModReleaseMetadata(
        "Loading Screen Tips v$project.version",
        file("$rootDir/CHANGELOG.md"),
        ["1.20"],
        ["fabric", "quilt"],
        "remapJar",
        ["remapSourcesJar"],
        "v${project.version}",
        false
)
modrinthInfo(mod, "loadingscreentips", [], [], [], ["midnightlib"])
githubInfo(mod, "UltrusBot", "LoadingScreenTips", "1.20")
curseforgeInfo(mod, "527804", ["midnightlib"])

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

jar {
    from("LICENSE") {
        rename { "LICENSE_${archives_base_name}" }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}



java {
    withSourcesJar()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}