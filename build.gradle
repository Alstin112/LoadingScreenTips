plugins {
    id 'fabric-loom' version '0.11.+'
    id 'maven-publish'
    id 'io.github.juuxel.loom-quiltflower' version '1.6.0'
    id 'org.quiltmc.quilt-mappings-on-loom' version '4.0.0'
    id "com.github.breadmoirai.github-release" version "2.4.1"
    id "com.modrinth.minotaur" version "2.+"
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name 'Quilt'
        url 'https://maven.quiltmc.org/repository'
    }
    maven {
        name 'Quilt Snapshots'
        url 'https://maven.quiltmc.org/repository/snapshot'
    }
    maven { url = "https://jitpack.io" }
    maven {
        url = "https://api.modrinth.com/maven"
    }
    maven {
        url "https://maven.terraformersmc.com/"
    }
}


dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings(loom.layered {
        addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${minecraft_version}+build.${project.quilt_mappings}:v2"))
    })
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    //TODO: Config next update
    modImplementation include("maven.modrinth:midnightlib:${project.midnightlib_version}")

    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

githubRelease {
    if (System.getenv("GH_TOKEN") != null) {
        authorization = "Token ${System.getenv("GH_TOKEN")}"
    }
    owner = "UltrusBot"
    repo = "LoadingScreenTips"
    targetCommitish = "1.19"
    tagName = "v${project.version}"
    body = new File("$rootDir/CHANGELOG.md").getText('UTF-8')
    releaseAssets = [remapJar, remapSourcesJar]
}

modrinth {
    if (System.getenv("MODRINTH_TOKEN") != null) {
        token = System.getenv("MODRINTH_TOKEN")
    }
    projectId = "loadingscreentips"
    versionType = "release"
    versionName = "Loading Screen Tips v${project.version}"
    uploadFile = remapJar
    changelog = new File("$rootDir/CHANGELOG.md").getText('UTF-8')
    versionName = "Loading Screen Tips v${project.version}"
    gameVersions = ["1.19.2"]
    loaders = ["quilt", "fabric"]
    dependencies {
        embedded.project "midnightlib"
    }
    syncBodyFrom = rootProject.file("README.md").text
}



curseforge {
    if (System.getenv("CF_API_KEY") != null) {
        apiKey = System.getenv("CF_API_KEY")

    }
    project {
        id = "527804"
        changelogType = "markdown"
        changelog = new File("$rootDir/CHANGELOG.md").getText('UTF-8')
        releaseType = "release"
        addGameVersion "1.19.2"
        mainArtifact(remapJar) {
            displayName = "Loading Screen Tips v${project.version}"
        }
    }

}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}